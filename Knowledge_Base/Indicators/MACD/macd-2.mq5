//+------------------------------------------------------------------+
//|                                                       MACD-2.mq5 |
//|                      Copyright \xa9 2005, MetaQuotes Software Corp. |
//|                                        http://www.metaquotes.net |
//+------------------------------------------------------------------+
#property copyright "Copyright \xa9 2005, MetaQuotes Software Corp."
#property link      "http://www.metaquotes.net"
//---- \xed\xee\xec\xe5\xf0 \xe2\xe5\xf0\xf1\xe8\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
#property version   "1.00"
#property description "MACD-2"
//---- \xed\xee\xec\xe5\xf0 \xe2\xe5\xf0\xf1\xe8\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
//---- \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe2 \xee\xf2\xe4\xe5\xeb\xfc\xed\xee\xec \xee\xea\xed\xe5
#property indicator_separate_window
//---- \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xee \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xf5 \xe1\xf3\xf4\xe5\xf0\xee\xe2 4
#property indicator_buffers 4
//---- \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xee \xe4\xe2\xe0 \xe3\xf0\xe0\xf4\xe8\xf7\xe5\xf1\xea\xe8\xf5 \xef\xee\xf1\xf2\xf0\xee\xe5\xed\xe8\xff
#property indicator_plots   2
//+-----------------------------------+
//| \xce\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xea\xee\xed\xf1\xf2\xe0\xed\xf2               |
//+-----------------------------------+
#define RESET  0 // \xea\xee\xed\xf1\xf2\xe0\xed\xf2\xe0 \xe4\xeb\xff \xe2\xee\xe7\xe2\xf0\xe0\xf2\xe0 \xf2\xe5\xf0\xec\xe8\xed\xe0\xeb\xf3 \xea\xee\xec\xe0\xed\xe4\xfb \xed\xe0 \xef\xe5\xf0\xe5\xf1\xf7\xe5\xf2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
//+-----------------------------------+
//| \xcf\xe0\xf0\xe0\xec\xe5\xf2\xf0\xfb \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0    |
//+-----------------------------------+
//---- \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe2 \xe2\xe8\xe4\xe5 \xf6\xe2\xe5\xf2\xed\xee\xe3\xee \xee\xe1\xeb\xe0\xea\xe0
#property indicator_type1   DRAW_FILLING
//---- \xe2 \xea\xe0\xf7\xe5\xf1\xf2\xe2\xe5 \xf6\xe2\xe5\xf2\xee\xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xfb
#property indicator_color1  clrLime,clrDeepPink
//---- \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xe5 \xec\xe5\xf2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
#property indicator_label1  "MACD_Cloud"
//+----------------------------------------------+
//| \xcf\xe0\xf0\xe0\xec\xe5\xf2\xf0\xfb \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 2             |
//+----------------------------------------------+
//---- \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe2 \xe2\xe8\xe4\xe5 \xf7\xe5\xf2\xfb\xf0\xe5\xf5\xf6\xe2\xe5\xf2\xed\xee\xe9 \xe3\xe8\xf1\xf2\xee\xe3\xf0\xe0\xec\xec\xfb
#property indicator_type2 DRAW_COLOR_HISTOGRAM
//---- \xe2 \xea\xe0\xf7\xe5\xf1\xf2\xe2\xe5 \xf6\xe2\xe5\xf2\xee\xe2 \xef\xff\xf2\xe8\xf6\xe2\xe5\xf2\xed\xee\xe9 \xe3\xe8\xf1\xf2\xee\xe3\xf0\xe0\xec\xec\xfb \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xfb
#property indicator_color2 clrBrown,clrViolet,clrGray,clrDeepSkyBlue,clrBlue
//---- \xeb\xe8\xed\xe8\xff \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 - \xf1\xef\xeb\xee\xf8\xed\xe0\xff
#property indicator_style2 STYLE_SOLID
//---- \xf2\xee\xeb\xf9\xe8\xed\xe0 \xeb\xe8\xed\xe8\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xf0\xe0\xe2\xed\xe0 2
#property indicator_width2 2
//---- \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xe5 \xec\xe5\xf2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
#property indicator_label2  "MACD"
//+-----------------------------------+
//| \xc2\xf5\xee\xe4\xed\xfb\xe5 \xef\xe0\xf0\xe0\xec\xe5\xf2\xf0\xfb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0      |
//+-----------------------------------+
input uint FastMACD     = 12;
input uint SlowMACD     = 26;
input uint SignalMACD   = 9;
input ENUM_APPLIED_PRICE   PriceMACD=PRICE_CLOSE;
//+-----------------------------------+
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xf6\xe5\xeb\xee\xf7\xe8\xf1\xeb\xe5\xed\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xe4\xe0\xed\xed\xfb\xf5
int  min_rates_total;
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xe8\xf5 \xec\xe0\xf1\xf1\xe8\xe2\xee\xe2, \xea\xee\xf2\xee\xf0\xfb\xe5 \xe1\xf3\xe4\xf3\xf2 \xe2
//---- \xe4\xe0\xeb\xfc\xed\xe5\xe9\xf8\xe5\xec \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xfb \xe2 \xea\xe0\xf7\xe5\xf1\xf2\xe2\xe5 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xf5 \xe1\xf3\xf4\xe5\xf0\xee\xe2
double ExtABuffer[],ExtBBuffer[];
double IndBuffer[],ColorIndBuffer[];
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xf6\xe5\xeb\xee\xf7\xe8\xf1\xeb\xe5\xed\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5 \xe4\xeb\xff \xf5\xe5\xed\xe4\xeb\xee\xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xee\xe2
int MACD_Handle;
//+------------------------------------------------------------------+
//| Custom indicator initialization function                         |
//+------------------------------------------------------------------+
int OnInit()
  {
//---- \xe8\xed\xe8\xf6\xe8\xe0\xeb\xe8\xe7\xe0\xf6\xe8\xff \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xe4\xe0\xed\xed\xfb\xf5
   min_rates_total=int(SignalMACD+MathMax(FastMACD,SlowMACD));
//---- \xef\xee\xeb\xf3\xf7\xe5\xed\xe8\xe5 \xf5\xe5\xed\xe4\xeb\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD
   MACD_Handle=iMACD(NULL,0,FastMACD,SlowMACD,SignalMACD,PriceMACD);
   if(MACD_Handle==INVALID_HANDLE)
     {
      Print(" \xcd\xe5 \xf3\xe4\xe0\xeb\xee\xf1\xfc \xef\xee\xeb\xf3\xf7\xe8\xf2\xfc \xf5\xe5\xed\xe4\xeb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD");
      return(INIT_FAILED);
     }
//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xee\xe3\xee \xec\xe0\xf1\xf1\xe8\xe2\xe0 \xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xe9 \xe1\xf3\xf4\xe5\xf0
   SetIndexBuffer(0,ExtABuffer,INDICATOR_DATA);
//---- \xe8\xed\xe4\xe5\xea\xf1\xe0\xf6\xe8\xff \xfd\xeb\xe5\xec\xe5\xed\xf2\xee\xe2 \xe2 \xe1\xf3\xf4\xe5\xf0\xe5 \xea\xe0\xea \xe2 \xf2\xe0\xe9\xec\xf1\xe5\xf0\xe8\xe8
   ArraySetAsSeries(ExtABuffer,true);

//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xee\xe3\xee \xec\xe0\xf1\xf1\xe8\xe2\xe0 \xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xe9 \xe1\xf3\xf4\xe5\xf0
   SetIndexBuffer(1,ExtBBuffer,INDICATOR_DATA);
//---- \xe8\xed\xe4\xe5\xea\xf1\xe0\xf6\xe8\xff \xfd\xeb\xe5\xec\xe5\xed\xf2\xee\xe2 \xe2 \xe1\xf3\xf4\xe5\xf0\xe5 \xea\xe0\xea \xe2 \xf2\xe0\xe9\xec\xf1\xe5\xf0\xe8\xe8
   ArraySetAsSeries(ExtBBuffer,true);

//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xee\xe3\xee \xec\xe0\xf1\xf1\xe8\xe2\xe0 IndBuffer \xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xe9 \xe1\xf3\xf4\xe5\xf0
   SetIndexBuffer(2,IndBuffer,INDICATOR_DATA);
//---- \xe8\xed\xe4\xe5\xea\xf1\xe0\xf6\xe8\xff \xfd\xeb\xe5\xec\xe5\xed\xf2\xee\xe2 \xe2 \xe1\xf3\xf4\xe5\xf0\xe5 \xea\xe0\xea \xe2 \xf2\xe0\xe9\xec\xf1\xe5\xf0\xe8\xe8
   ArraySetAsSeries(IndBuffer,true);

//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xee\xe3\xee \xec\xe0\xf1\xf1\xe8\xe2\xe0 \xe2 \xf6\xe2\xe5\xf2\xee\xe2\xee\xe9, \xe8\xed\xe4\xe5\xea\xf1\xed\xfb\xe9 \xe1\xf3\xf4\xe5\xf0
   SetIndexBuffer(3,ColorIndBuffer,INDICATOR_COLOR_INDEX);
//---- \xe8\xed\xe4\xe5\xea\xf1\xe0\xf6\xe8\xff \xfd\xeb\xe5\xec\xe5\xed\xf2\xee\xe2 \xe2 \xe1\xf3\xf4\xe5\xf0\xe5 \xea\xe0\xea \xe2 \xf2\xe0\xe9\xec\xf1\xe5\xf0\xe8\xe8
   ArraySetAsSeries(ColorIndBuffer,true);

//---- \xee\xf1\xf3\xf9\xe5\xf1\xf2\xe2\xeb\xe5\xed\xe8\xe5 \xf1\xe4\xe2\xe8\xe3\xe0 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
   PlotIndexSetInteger(0,PLOT_DRAW_BEGIN,min_rates_total);
//---- \xee\xf1\xf3\xf9\xe5\xf1\xf2\xe2\xeb\xe5\xed\xe8\xe5 \xf1\xe4\xe2\xe8\xe3\xe0 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
   PlotIndexSetInteger(1,PLOT_DRAW_BEGIN,min_rates_total);

//--- \xf1\xee\xe7\xe4\xe0\xed\xe8\xe5 \xe8\xec\xe5\xed\xe8 \xe4\xeb\xff \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xff \xe2 \xee\xf2\xe4\xe5\xeb\xfc\xed\xee\xec \xef\xee\xe4\xee\xea\xed\xe5 \xe8 \xe2\xee \xe2\xf1\xef\xeb\xfb\xe2\xe0\xfe\xf9\xe5\xe9 \xef\xee\xe4\xf1\xea\xe0\xe7\xea\xe5
   IndicatorSetString(INDICATOR_SHORTNAME,"MACD-2");
//--- \xee\xef\xf0\xe5\xe4\xe5\xeb\xe5\xed\xe8\xe5 \xf2\xee\xf7\xed\xee\xf1\xf2\xe8 \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xff \xe7\xed\xe0\xf7\xe5\xed\xe8\xe9 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
   IndicatorSetInteger(INDICATOR_DIGITS,0);
//---- \xe7\xe0\xe2\xe5\xf0\xf8\xe5\xed\xe8\xe5 \xe8\xed\xe8\xf6\xe8\xe0\xeb\xe8\xe7\xe0\xf6\xe8\xe8
   return(INIT_SUCCEEDED);
  }
//+------------------------------------------------------------------+
//| Custom indicator iteration function                              |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,    // \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xee \xe8\xf1\xf2\xee\xf0\xe8\xe8 \xe2 \xe1\xe0\xf0\xe0\xf5 \xed\xe0 \xf2\xe5\xea\xf3\xf9\xe5\xec \xf2\xe8\xea\xe5
                const int prev_calculated,// \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xee \xe8\xf1\xf2\xee\xf0\xe8\xe8 \xe2 \xe1\xe0\xf0\xe0\xf5 \xed\xe0 \xef\xf0\xe5\xe4\xfb\xe4\xf3\xf9\xe5\xec \xf2\xe8\xea\xe5
                const datetime &Time[],
                const double &Open[],
                const double &High[],
                const double &Low[],
                const double &Close[],
                const long &Tick_Volume[],
                const long &Volume[],
                const int &Spread[])
  {
//---- \xef\xf0\xee\xe2\xe5\xf0\xea\xe0 \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xe0 \xe1\xe0\xf0\xee\xe2 \xed\xe0 \xe4\xee\xf1\xf2\xe0\xf2\xee\xf7\xed\xee\xf1\xf2\xfc \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0
   if(BarsCalculated(MACD_Handle)<rates_total || rates_total<min_rates_total) return(RESET);
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xff \xeb\xee\xea\xe0\xeb\xfc\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5
   int to_copy,limit;
//---- \xf0\xe0\xf1\xf7\xe5\xf2 \xf1\xf2\xe0\xf0\xf2\xee\xe2\xee\xe3\xee \xed\xee\xec\xe5\xf0\xe0 limit \xe4\xeb\xff \xf6\xe8\xea\xeb\xe0 \xef\xe5\xf0\xe5\xf1\xf7\xe5\xf2\xe0 \xe1\xe0\xf0\xee\xe2
   if(prev_calculated>rates_total || prev_calculated<=0)// \xef\xf0\xee\xe2\xe5\xf0\xea\xe0 \xed\xe0 \xef\xe5\xf0\xe2\xfb\xe9 \xf1\xf2\xe0\xf0\xf2 \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
      limit=rates_total-min_rates_total-1; // \xf1\xf2\xe0\xf0\xf2\xee\xe2\xfb\xe9 \xed\xee\xec\xe5\xf0 \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe2\xf1\xe5\xf5 \xe1\xe0\xf0\xee\xe2
   else limit=rates_total-prev_calculated;  // \xf1\xf2\xe0\xf0\xf2\xee\xe2\xfb\xe9 \xed\xee\xec\xe5\xf0 \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xf2\xee\xeb\xfc\xea\xee \xed\xee\xe2\xfb\xf5 \xe1\xe0\xf0\xee\xe2
//----
   to_copy=limit+1;
//---- \xea\xee\xef\xe8\xf0\xf3\xe5\xec \xe2\xed\xee\xe2\xfc \xef\xee\xff\xe2\xe8\xe2\xf8\xe8\xe5\xf1\xff \xe4\xe0\xed\xed\xfb\xe5 \xe2 \xec\xe0\xf1\xf1\xe8\xe2\xfb
   if(CopyBuffer(MACD_Handle,MAIN_LINE,0,to_copy,ExtABuffer)<=0) return(RESET);
   if(CopyBuffer(MACD_Handle,SIGNAL_LINE,0,to_copy,ExtBBuffer)<=0) return(RESET);
//---- \xee\xf1\xed\xee\xe2\xed\xee\xe9 \xf6\xe8\xea\xeb \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
   for(int bar=limit; bar>=0 && !IsStopped(); bar--)
     {
      ExtABuffer[bar]/=_Point;
      ExtBBuffer[bar]/=_Point;
      IndBuffer[bar]=3*(ExtABuffer[bar]-ExtBBuffer[bar]);
     }
//----
   if(prev_calculated>rates_total || prev_calculated<=0) limit--;
//---- \xee\xf1\xed\xee\xe2\xed\xee\xe9 \xf6\xe8\xea\xeb \xf0\xe0\xf1\xea\xf0\xe0\xf1\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 Ind
   for(int bar=limit; bar>=0 && !IsStopped(); bar--)
     {
      int clr=2;
      //----
      if(IndBuffer[bar]>0)
        {
         if(IndBuffer[bar]>IndBuffer[bar+1]) clr=4;
         if(IndBuffer[bar]<IndBuffer[bar+1]) clr=3;
        }
      //----
      if(IndBuffer[bar]<0)
        {
         if(IndBuffer[bar]<IndBuffer[bar+1]) clr=0;
         if(IndBuffer[bar]>IndBuffer[bar+1]) clr=1;
        }
      //----
      ColorIndBuffer[bar]=clr;
     }
//----
   return(rates_total);
  }
//+------------------------------------------------------------------+
