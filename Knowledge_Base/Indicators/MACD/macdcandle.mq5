//+------------------------------------------------------------------+
//|                                                   MACDCandle.mq5 |
//|                               Copyright \xa9 2013, Nikolay Kositsin |
//|                              Khabarovsk,   farria@mail.redcom.ru |
//+------------------------------------------------------------------+
#property copyright "Copyright \xa9 2013, Nikolay Kositsin"
#property link "farria@mail.redcom.ru"
#property description "MACDCandle Smoothed"
//---- \xed\xee\xec\xe5\xf0 \xe2\xe5\xf0\xf1\xe8\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
#property version   "1.00"
//+----------------------------------------------+
//| \xcf\xe0\xf0\xe0\xec\xe5\xf2\xf0\xfb \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0               |
//+----------------------------------------------+
//---- \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe2 \xee\xf2\xe4\xe5\xeb\xfc\xed\xee\xec \xee\xea\xed\xe5
#property indicator_separate_window
//---- \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe8 \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xee \xef\xff\xf2\xfc \xe1\xf3\xf4\xe5\xf0\xee\xe2
#property indicator_buffers 5
//---- \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xee \xe2\xf1\xe5\xe3\xee \xee\xe4\xed\xee \xe3\xf0\xe0\xf4\xe8\xf7\xe5\xf1\xea\xee\xe5 \xef\xee\xf1\xf2\xf0\xee\xe5\xed\xe8\xe5
#property indicator_plots   1
//---- \xe2 \xea\xe0\xf7\xe5\xf1\xf2\xe2\xe5 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xfb \xf6\xe2\xe5\xf2\xed\xfb\xe5 \xf1\xe2\xe5\xf7\xe8
#property indicator_type1   DRAW_COLOR_CANDLES
#property indicator_color1   clrDeepPink,clrBlue,clrTeal
//---- \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xe5 \xec\xe5\xf2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
#property indicator_label1  "MACDCandle Open;MACDCandle High;MACDCandle Low;MACDCandle Close"
//+----------------------------------------------+
//| \xce\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xea\xee\xed\xf1\xf2\xe0\xed\xf2                          |
//+----------------------------------------------+
#define RESET  0 // \xea\xee\xed\xf1\xf2\xe0\xed\xf2\xe0 \xe4\xeb\xff \xe2\xee\xe7\xe2\xf0\xe0\xf2\xe0 \xf2\xe5\xf0\xec\xe8\xed\xe0\xeb\xf3 \xea\xee\xec\xe0\xed\xe4\xfb \xed\xe0 \xef\xe5\xf0\xe5\xf1\xf7\xe5\xf2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
//+----------------------------------------------+
//| \xce\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xef\xe5\xf0\xe5\xf7\xe8\xf1\xeb\xe5\xed\xe8\xe9                      |
//+----------------------------------------------+
enum MODE
  {
   MODE_HISTOGRAM=0,    // \xc3\xe8\xf1\xf2\xee\xe3\xf0\xe0\xec\xec\xe0
   MODE_SIGNAL_LINE=1   // \xd1\xe8\xe3\xed\xe0\xeb\xfc\xed\xe0\xff \xeb\xe8\xed\xe8\xff
  };
//+----------------------------------------------+
//| \xc2\xf5\xee\xe4\xed\xfb\xe5 \xef\xe0\xf0\xe0\xec\xe5\xf2\xf0\xfb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0                 |
//+----------------------------------------------+
input uint  fast_ema_period=12;             // \xcf\xe5\xf0\xe8\xee\xe4 \xe1\xfb\xf1\xf2\xf0\xee\xe9 \xf1\xf0\xe5\xe4\xed\xe5\xe9
input uint  slow_ema_period=26;             // \xcf\xe5\xf0\xe8\xee\xe4 \xec\xe5\xe4\xeb\xe5\xed\xed\xee\xe9 \xf1\xf0\xe5\xe4\xed\xe5\xe9
input uint  signal_period=9;                // \xcf\xe5\xf0\xe8\xee\xe4 \xf3\xf1\xf0\xe5\xe4\xed\xe5\xed\xe8\xff \xf0\xe0\xe7\xed\xee\xf1\xf2\xe8
input MODE  mode=MODE_SIGNAL_LINE;          // \xc8\xf1\xf2\xee\xf7\xed\xe8\xea \xe4\xe0\xed\xfb\xf5 \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0
//+----------------------------------------------+
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xe8\xf5 \xec\xe0\xf1\xf1\xe8\xe2\xee\xe2, \xea\xee\xf2\xee\xf0\xfb\xe5 \xe1\xf3\xe4\xf3\xf2 \xe2
//---- \xe4\xe0\xeb\xfc\xed\xe5\xe9\xf8\xe5\xec \xe8\xf1\xef\xee\xeb\xfc\xe7\xee\xe2\xe0\xed\xfb \xe2 \xea\xe0\xf7\xe5\xf1\xf2\xe2\xe5 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xf5 \xe1\xf3\xf4\xe5\xf0\xee\xe2
double ExtOpenBuffer[];
double ExtHighBuffer[];
double ExtLowBuffer[];
double ExtCloseBuffer[];
double ExtColorBuffer[];
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xf6\xe5\xeb\xee\xf7\xe8\xf1\xeb\xe5\xed\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xe4\xe0\xed\xed\xfb\xf5
int min_rates_total;
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xf6\xe5\xeb\xee\xf7\xe8\xf1\xeb\xe5\xed\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5 \xe4\xeb\xff \xf5\xe5\xed\xe4\xeb\xee\xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xee\xe2
int MACD_Handle[4];
//+------------------------------------------------------------------+
//| Custom indicator initialization function                         |
//+------------------------------------------------------------------+
int OnInit()
  {
//---- \xe8\xed\xe8\xf6\xe8\xe0\xeb\xe8\xe7\xe0\xf6\xe8\xff \xe3\xeb\xee\xe1\xe0\xeb\xfc\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5
   min_rates_total=int(MathMax(fast_ema_period,slow_ema_period));
   if(mode==MODE_SIGNAL_LINE) min_rates_total+=int(signal_period);
//---- \xef\xee\xeb\xf3\xf7\xe5\xed\xe8\xe5 \xf5\xe5\xed\xe4\xeb\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD
   MACD_Handle[0]=iMACD(NULL,0,fast_ema_period,slow_ema_period,signal_period,PRICE_OPEN);
   if(MACD_Handle[0]==INVALID_HANDLE)
     {
      Print(" \xcd\xe5 \xf3\xe4\xe0\xeb\xee\xf1\xfc \xef\xee\xeb\xf3\xf7\xe8\xf2\xfc \xf5\xe5\xed\xe4\xeb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD["+string(0)+"]!");
      return(INIT_FAILED);
     }
   MACD_Handle[1]=iMACD(NULL,0,fast_ema_period,slow_ema_period,signal_period,PRICE_HIGH);
   if(MACD_Handle[1]==INVALID_HANDLE)
     {
      Print(" \xcd\xe5 \xf3\xe4\xe0\xeb\xee\xf1\xfc \xef\xee\xeb\xf3\xf7\xe8\xf2\xfc \xf5\xe5\xed\xe4\xeb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD["+string(1)+"]!");
      return(INIT_FAILED);
     }
   MACD_Handle[2]=iMACD(NULL,0,fast_ema_period,slow_ema_period,signal_period,PRICE_LOW);
   if(MACD_Handle[2]==INVALID_HANDLE)
     {
      Print(" \xcd\xe5 \xf3\xe4\xe0\xeb\xee\xf1\xfc \xef\xee\xeb\xf3\xf7\xe8\xf2\xfc \xf5\xe5\xed\xe4\xeb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD["+string(2)+"]!");
      return(INIT_FAILED);
     }
   MACD_Handle[3]=iMACD(NULL,0,fast_ema_period,slow_ema_period,signal_period,PRICE_CLOSE);
   if(MACD_Handle[3]==INVALID_HANDLE)
     {
      Print(" \xcd\xe5 \xf3\xe4\xe0\xeb\xee\xf1\xfc \xef\xee\xeb\xf3\xf7\xe8\xf2\xfc \xf5\xe5\xed\xe4\xeb \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 iMACD["+string(3)+"]!");
      return(INIT_FAILED);
     }
//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xe8\xf5 \xec\xe0\xf1\xf1\xe8\xe2\xee\xe2 \xe2 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xed\xfb\xe5 \xe1\xf3\xf4\xe5\xf0\xfb
   SetIndexBuffer(0,ExtOpenBuffer,INDICATOR_DATA);
   SetIndexBuffer(1,ExtHighBuffer,INDICATOR_DATA);
   SetIndexBuffer(2,ExtLowBuffer,INDICATOR_DATA);
   SetIndexBuffer(3,ExtCloseBuffer,INDICATOR_DATA);
//---- \xef\xf0\xe5\xe2\xf0\xe0\xf9\xe5\xed\xe8\xe5 \xe4\xe8\xed\xe0\xec\xe8\xf7\xe5\xf1\xea\xee\xe3\xee \xec\xe0\xf1\xf1\xe8\xe2\xe0 \xe2 \xf6\xe2\xe5\xf2\xee\xe2\xee\xe9, \xe8\xed\xe4\xe5\xea\xf1\xed\xfb\xe9 \xe1\xf3\xf4\xe5\xf0
   SetIndexBuffer(4,ExtColorBuffer,INDICATOR_COLOR_INDEX);
//---- \xe8\xed\xe4\xe5\xea\xf1\xe0\xf6\xe8\xff \xfd\xeb\xe5\xec\xe5\xed\xf2\xee\xe2 \xe2 \xe1\xf3\xf4\xe5\xf0\xe0\xf5 \xea\xe0\xea \xe2 \xf2\xe0\xe9\xec\xf1\xe5\xf0\xe8\xff\xf5
   ArraySetAsSeries(ExtOpenBuffer,true);
   ArraySetAsSeries(ExtHighBuffer,true);
   ArraySetAsSeries(ExtLowBuffer,true);
   ArraySetAsSeries(ExtCloseBuffer,true);
   ArraySetAsSeries(ExtColorBuffer,true);
//---- \xee\xf1\xf3\xf9\xe5\xf1\xf2\xe2\xeb\xe5\xed\xe8\xe5 \xf1\xe4\xe2\xe8\xe3\xe0 \xed\xe0\xf7\xe0\xeb\xe0 \xee\xf2\xf1\xf7\xe5\xf2\xe0 \xee\xf2\xf0\xe8\xf1\xee\xe2\xea\xe8 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0 1
   PlotIndexSetInteger(4,PLOT_DRAW_BEGIN,min_rates_total);
//---- \xf3\xf1\xf2\xe0\xed\xee\xe2\xea\xe0 \xf4\xee\xf0\xec\xe0\xf2\xe0 \xf2\xee\xf7\xed\xee\xf1\xf2\xe8 \xee\xf2\xee\xe1\xf0\xe0\xe6\xe5\xed\xe8\xff \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
   IndicatorSetInteger(INDICATOR_DIGITS,0);
//---- \xe8\xec\xff \xe4\xeb\xff \xee\xea\xee\xed \xe4\xe0\xed\xed\xfb\xf5 \xe8 \xec\xe5\xf2\xea\xe0 \xe4\xeb\xff \xef\xee\xe4\xee\xea\xee\xed
   string short_name="MACDCandl";
   IndicatorSetString(INDICATOR_SHORTNAME,short_name);
//---- \xe7\xe0\xe2\xe5\xf0\xf8\xe5\xed\xe8\xe5 \xe8\xed\xe8\xf6\xe8\xe0\xeb\xe8\xe7\xe0\xf6\xe8\xe8
   return(INIT_SUCCEEDED);
  }
//+------------------------------------------------------------------+
//| Custom indicator iteration function                              |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,
                const int prev_calculated,
                const datetime &time[],
                const double &open[],
                const double &high[],
                const double &low[],
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[])
  {
//---- \xef\xf0\xee\xe2\xe5\xf0\xea\xe0 \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xe0 \xe1\xe0\xf0\xee\xe2 \xed\xe0 \xe4\xee\xf1\xf2\xe0\xf2\xee\xf7\xed\xee\xf1\xf2\xfc \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0
   if(BarsCalculated(MACD_Handle[0])<rates_total
      || BarsCalculated(MACD_Handle[1])<rates_total
      || BarsCalculated(MACD_Handle[2])<rates_total
      || BarsCalculated(MACD_Handle[3])<rates_total
      || rates_total<min_rates_total)
      return(RESET);
//---- \xee\xe1\xfa\xff\xe2\xeb\xe5\xed\xe8\xe5 \xeb\xee\xea\xe0\xeb\xfc\xed\xfb\xf5 \xef\xe5\xf0\xe5\xec\xe5\xed\xed\xfb\xf5
   int to_copy,limit,bar;
//---- \xf0\xe0\xf1\xf7\xe5\xf2\xfb \xed\xe5\xee\xe1\xf5\xee\xe4\xe8\xec\xee\xe3\xee \xea\xee\xeb\xe8\xf7\xe5\xf1\xf2\xe2\xe0 \xea\xee\xef\xe8\xf0\xf3\xe5\xec\xfb\xf5 \xe4\xe0\xed\xed\xfb\xf5 \xe8 \xf1\xf2\xe0\xf0\xf2\xee\xe2\xee\xe3\xee \xed\xee\xec\xe5\xf0\xe0 limit \xe4\xeb\xff \xf6\xe8\xea\xeb\xe0 \xef\xe5\xf0\xe5\xf1\xf7\xe5\xf2\xe0 \xe1\xe0\xf0\xee\xe2
   if(prev_calculated>rates_total || prev_calculated<=0)// \xef\xf0\xee\xe2\xe5\xf0\xea\xe0 \xed\xe0 \xef\xe5\xf0\xe2\xfb\xe9 \xf1\xf2\xe0\xf0\xf2 \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe8\xed\xe4\xe8\xea\xe0\xf2\xee\xf0\xe0
     {
      limit=rates_total-1; // \xf1\xf2\xe0\xf0\xf2\xee\xe2\xfb\xe9 \xed\xee\xec\xe5\xf0 \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xe2\xf1\xe5\xf5 \xe1\xe0\xf0\xee\xe2
     }
   else
     {
      limit=rates_total-prev_calculated; // \xf1\xf2\xe0\xf0\xf2\xee\xe2\xfb\xe9 \xed\xee\xec\xe5\xf0 \xe4\xeb\xff \xf0\xe0\xf1\xf7\xe5\xf2\xe0 \xed\xee\xe2\xfb\xf5 \xe1\xe0\xf0\xee\xe2
     }
//---
   to_copy=limit+1;
//---- \xea\xee\xef\xe8\xf0\xf3\xe5\xec \xe2\xed\xee\xe2\xfc \xef\xee\xff\xe2\xe8\xe2\xf8\xe8\xe5\xf1\xff \xe4\xe0\xed\xed\xfb\xe5 \xe2 \xec\xe0\xf1\xf1\xe8\xe2\xfb
   if(CopyBuffer(MACD_Handle[0],int(mode),0,to_copy,ExtOpenBuffer)<=0) return(RESET);
   if(CopyBuffer(MACD_Handle[1],int(mode),0,to_copy,ExtHighBuffer)<=0) return(RESET);
   if(CopyBuffer(MACD_Handle[2],int(mode),0,to_copy,ExtLowBuffer)<=0) return(RESET);
   if(CopyBuffer(MACD_Handle[3],int(mode),0,to_copy,ExtCloseBuffer)<=0) return(RESET);
//---- \xee\xf1\xed\xee\xe2\xed\xee\xe9 \xf6\xe8\xea\xeb \xe8\xf1\xef\xf0\xe0\xe2\xeb\xe5\xed\xe8\xff \xe8 \xee\xea\xf0\xe0\xf8\xe8\xe2\xe0\xed\xe8\xff \xf1\xe2\xe5\xf7\xe5\xe9
   for(bar=limit; bar>=0 && !IsStopped(); bar--)
     {
      double Max=MathMax(ExtOpenBuffer[bar],ExtCloseBuffer[bar]);
      double Min=MathMin(ExtOpenBuffer[bar],ExtCloseBuffer[bar]);
      //---
      ExtHighBuffer[bar]=MathMax(Max,ExtHighBuffer[bar])/_Point;
      ExtLowBuffer[bar]=MathMin(Min,ExtLowBuffer[bar])/_Point;
      //---
      ExtOpenBuffer[bar]/=_Point;
      ExtCloseBuffer[bar]/=_Point;
      //---
      if(ExtOpenBuffer[bar]<ExtCloseBuffer[bar]) ExtColorBuffer[bar]=2.0;
      else if(ExtOpenBuffer[bar]>ExtCloseBuffer[bar]) ExtColorBuffer[bar]=0.0;
      else ExtColorBuffer[bar]=1.0;
     }
//----
   return(rates_total);
  }
//+------------------------------------------------------------------+
